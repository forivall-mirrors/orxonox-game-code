# - Try to find ogg/vorbis
# Once done this will define
#
#  OGG_FOUND - system has ogg
#  OGG_INCLUDE_DIR
#  OGG_LIBRARY
#  VORBIS_FOUND - system has vorbis
#  VORBIS_INCLUDE_DIR
#  VORBIS_LIBRARY
#  VORBISENC_LIBRARY
#  VORBISFILE_LIBRARY
#
# $OGGDIR and $VORBISDIR are environment variables used
# for finding ogg/vorbis.
#
# Several changes and additions by Fabian 'x3n' Landau
# Most of all rewritten by Adrian Friedli
#                 > www.orxonox.net <

INCLUDE(FindPackageHandleStandardArgs)
INCLUDE(HandleLibraryTypes)

SET(OGG_SEARCH_PATHS
  $ENV{OGGDIR}
  $ENV{VORBISDIR}
  /usr/local
  /usr
  /sw
  /opt/local
  /opt/csw
  /opt
)

FIND_PATH(OGG_INCLUDE_DIR ogg/ogg.h
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES include
)
FIND_PATH(VORBIS_INCLUDE_DIR vorbis/codec.h
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES include
)
FIND_LIBRARY(OGG_LIBRARY_OPTIMIZED NAMES ogg
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES lib
)
FIND_LIBRARY(OGG_LIBRARY_DEBUG NAMES ogg${LIBRARY_DEBUG_POSTFIX}
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES lib
)
FIND_LIBRARY(VORBIS_LIBRARY_OPTIMIZED NAMES vorbis
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES lib
)
FIND_LIBRARY(VORBIS_LIBRARY_DEBUG NAMES vorbis${LIBRARY_DEBUG_POSTFIX}
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES lib
)
FIND_LIBRARY(VORBISFILE_LIBRARY_OPTIMIZED NAMES vorbisfile
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES lib
)
FIND_LIBRARY(VORBISFILE_LIBRARY_DEBUG NAMES vorbisfile${LIBRARY_DEBUG_POSTFIX}
  PATHS ${OGG_SEARCH_PATHS}
  PATH_SUFFIXES lib
)

# handle the QUIETLY and REQUIRED arguments and set *_FOUND to TRUE if
# all listed variables are TRUE
FIND_PACKAGE_HANDLE_STANDARD_ARGS(OGG DEFAULT_MSG
  OGG_LIBRARY_OPTIMIZED
  OGG_INCLUDE_DIR
)
SET(VORBIS_FIND_REQUIRED ON)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(VORBIS DEFAULT_MSG
  VORBIS_LIBRARY_OPTIMIZED
  VORBIS_INCLUDE_DIR
)
SET(VORBISFILE_FIND_REQUIRED ON)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(VORBISFILE DEFAULT_MSG
  VORBISFILE_LIBRARY_OPTIMIZED
  VORBIS_INCLUDE_DIR
)

# Set optimized and debug libraries
HandleLibraryTypes(OGG)
HandleLibraryTypes(VORBIS)
HandleLibraryTypes(VORBISFILE)

MARK_AS_ADVANCED(
  OGG_LIBRARY
  OGG_LIBRARY_OPTIMIZED
  OGG_LIBRARY_DEBUG
  OGG_INCLUDE_DIR
  VORBIS_LIBRARY
  VORBIS_LIBRARY_OPTIMIZED
  VORBIS_LIBRARY_DEBUG
  VORBISFILE_LIBRARY
  VORBISFILE_LIBRARY_OPTIMIZED
  VORBISFILE_LIBRARY_DEBUG
  VORBIS_INCLUDE_DIR
)
